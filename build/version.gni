# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

# This exposes the Chrome version and Crosswalk version as GN variables for
# use in build files.
#
# PREFER NOT TO USE THESE. The GYP build uses this kind of thing extensively.
# However, it is far better to write an action (or use the process_version
# wrapper in chrome/version.gni) to generate a file at build-time with the
# information you need. This allows better dependency checking and GN will
# run faster.
#
# These values should only be used if you REALLY need to depend on them at
# build-time, for example, in the computation of output file names.

# Give version.py a pattern that will expand to a GN scope consisting of
# all values we need at once.

_version_template = "full = \"@MAJOR@.@MINOR@.@BUILD@.@PATCH@\""

# The file containing the Chrome version number.
chrome_version_file = "//chrome/VERSION"

_result = exec_script("//build/util/version.py",
                      [
                        "-f",
                        rebase_path(chrome_version_file, root_build_dir),
                        "-t",
                        _version_template,
                      ],
                      "scope",
                      [ chrome_version_file ])
chrome_version = _result.full

# The file containing the Crosswalk version number.
xwalk_version_file = "//xwalk/VERSION"

_result = exec_script("//build/util/version.py",
                      [
                        "-f",
                        rebase_path(xwalk_version_file, root_build_dir),
                        "-t",
                        _version_template,
                      ],
                      "scope",
                      [ xwalk_version_file ])
xwalk_version = _result.full

if (is_android) {
  import("//build/config/android/config.gni")

  # Get xwalk API version
  _xwalk_api_version_template = "api = \"@API@\" min_api = \"@MIN_API@\""
  xwalk_api_version_file = "//xwalk/API_VERSION"
  _result = exec_script("//build/util/version.py",
                        [
                          "-f",
                          rebase_path(xwalk_api_version_file, root_build_dir),
                          "-t",
                          _xwalk_api_version_template,
                        ],
                        "scope",
                        [ xwalk_api_version_file ])
  api_version = _result.api
  min_api_version = _result.min_api

  # Get xwalk_version_code
  _result = exec_script("//xwalk/build/android/generate_version_code.py",
                        [
                          "--abi-name=$android_app_abi",
                          "--version=$xwalk_version",
                        ],
                        "trim string",
                        [ xwalk_version_file ])
  xwalk_version_code = _result
}
