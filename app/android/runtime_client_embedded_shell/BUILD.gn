# Copyright (c) 2016 Intel Corporation. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/android/rules.gni")

android_apk("xwalk_runtime_client_embedded_shell_apk") {
  apk_name = "XWalkRuntimeClientEmbeddedShell"
  android_manifest = "AndroidManifest.xml"
  native_libs = [
    "$root_out_dir/libxwalkdummy.so",
    "$root_out_dir/libxwalkcore.so",
    "$root_out_dir/libecho_extension.so",
  ]
  deps = [
    ":xwalk_runtime_client_embedded_shell_apk_assets",
    ":xwalk_runtime_client_embedded_shell_apk_java",
    ":xwalk_runtime_client_embedded_shell_apk_pak",
    ":xwalk_runtime_client_embedded_shell_apk_resources",
    "//base:base_java",
    "//xwalk/extensions/test:echo_extension",
    "//xwalk/runtime/android/core_internal:xwalk_core_internal_java",
    "//xwalk/runtime/android/dummy_lib:libxwalkdummy",
    "//xwalk/runtime/app/android:libxwalkcore",
  ]
}

android_library("xwalk_runtime_client_embedded_shell_apk_java") {
  java_files = [ "src/org/xwalk/runtime/client/embedded/shell/XWalkRuntimeClientEmbeddedShellActivity.java" ]
  deps = [
    ":xwalk_runtime_client_embedded_shell_apk_resources",
    "//base:base_java",
    "//content/public/android:content_java",
    "//xwalk/app/android/runtime_client:xwalk_app_runtime_java",
    "//xwalk/runtime/android/core:xwalk_core_java",
  ]
}

android_assets("xwalk_runtime_client_embedded_shell_apk_pak") {
  sources = [
    "$root_out_dir/xwalk.pak",
    "$root_out_dir/xwalk_100_percent.pak",
  ]
  deps = [
    "//third_party/icu:icu_assets",
    "//v8:v8_external_startup_data_assets",
    "//xwalk/resources:xwalk_pak",
    "//xwalk/resources:xwalk_resources_100_percent_pak",
  ]
  disable_compression = true
}

android_assets("xwalk_runtime_client_embedded_shell_apk_assets") {
  sources = []
  deps = [
    "//third_party/icu:icu_assets",
    "//xwalk/resources:xwalk_pak",
    "//xwalk/resources:xwalk_resources_100_percent_pak",
  ]
  renaming_sources = [
    "//xwalk/test/android/data/manifest.json",
    "//xwalk/test/android/data/index.html",
    "//xwalk/test/android/data/sampapp-icon-helloworld.png",
    "//xwalk/test/android/data/myextension/myextension.js",
    "//xwalk/test/android/data/myextension/myextension.json",
    "//xwalk/experimental/launch_screen/launch_screen_api.js",
    "//xwalk/experimental/wifidirect/wifidirect_api.js",
    "//xwalk/test/android/data/www/manifest_self.json",
    "//xwalk/test/android/data/www/manifest_inline_script.json",
    "//xwalk/test/android/data/www/cross_origin.html",
    "//xwalk/test/android/data/www/csp.html",
    "//xwalk/test/android/data/www/manifest_without_xwalk_hosts.json",
    "//xwalk/test/android/data/www/manifest_xwalk_hosts.json",
  ]
  renaming_destinations = [
    "manifest.json",
    "index.html",
    "sampapp-icon-helloworld.png",
    "xwalk-extensions/myextension/myextension.js",
    "xwalk-extensions/myextension/myextension.json",
    "jsapi/launch_screen_api.js",
    "jsapi/wifidirect_api.js",
    "www/manifest_self.json",
    "www/manifest_inline_script.json",
    "www/cross_origin.html",
    "www/csp.html",
    "www/manifest_without_xwalk_hosts.json",
    "www/manifest_xwalk_hosts.json",
  ]
}

android_resources("xwalk_runtime_client_embedded_shell_apk_resources") {
  resource_dirs = [ "res" ]
  custom_package = "org.xwalk.runtime.client.embedded.shell"
}
