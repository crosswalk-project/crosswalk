import("//build/config/android/rules.gni")

android_apk("xwalk_runtime_client_shell_apk") {
  deps = [
    ":xwalk_runtime_client_shell_apk_assets",
    ":xwalk_runtime_client_shell_apk_java",
    ":xwalk_runtime_client_shell_apk_resources",
    "//base:base_java",
    "//xwalk/extensions/test:echo_extension",
    "//xwalk/runtime/android/dummy_lib:libxwalkdummy",
  ]
  apk_name = "XWalkRuntimeClientShell"
  android_manifest =
      "//xwalk/app/android/runtime_client_shell/AndroidManifest.xml"
  native_libs = [
    "$root_out_dir/libxwalkdummy.so",
    "$root_out_dir/extensions/test/echo_extension/libecho_extension.so",
  ]
}

android_library("xwalk_runtime_client_shell_apk_java") {
  deps = [
    ":xwalk_runtime_client_shell_apk_resources",
    "//xwalk/app/android/runtime_client:xwalk_app_runtime_java",
    "//xwalk/runtime/android/core:xwalk_core_java",
  ]
  java_files = [ "//xwalk/app/android/runtime_client_shell/src/org/xwalk/runtime/client/shell/XWalkRuntimeClientShellActivity.java" ]
}

android_assets("xwalk_runtime_client_shell_apk_assets") {
  renaming_sources = [
    "//xwalk/test/android/data/www/manifest_self.json",
    "//xwalk/test/android/data/www/manifest_inline_script.json",
    "//xwalk/test/android/data/www/cross_origin.html",
    "//xwalk/test/android/data/www/csp.html",
    "//xwalk/test/android/data/www/manifest_without_xwalk_hosts.json",
    "//xwalk/test/android/data/www/manifest_xwalk_hosts.json",
    "//xwalk/test/android/data/manifest.json",
    "//xwalk/test/android/data/index.html",
    "//xwalk/test/android/data/sampapp-icon-helloworld.png",
    "//xwalk/test/android/data/myextension/myextension.js",
    "//xwalk/test/android/data/myextension/myextension.json",
  ]
  renaming_destinations = [
    "www/manifest_self.json",
    "www/manifest_inline_script.json",
    "www/cross_origin.html",
    "www/csp.html",
    "www/manifest_without_xwalk_hosts.json",
    "www/manifest_xwalk_hosts.json",
    "manifest.json",
    "index.html",
    "sampapp-icon-helloworld.png",
    "xwalk-extensions/myextension/myextension.js",
    "xwalk-extensions/myextension/myextension.json",
  ]
  disable_compression = true
}

android_resources("xwalk_runtime_client_shell_apk_resources") {
  resource_dirs = [ "//xwalk/app/android/runtime_client_shell/res" ]
  custom_package = "org.xwalk.runtime.client.shell"
}
